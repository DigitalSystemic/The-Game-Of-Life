0→θ:0→R
0→S:0→W
ClrHome
Output(3,2,"MOOGGY PRESENTS:"
Output(5,2,"THE GAME OF LIFE"
Output(7,2,"MATRIX INITIALIZATION"
Output(8,2,"WAIT PLEASE"
For(X,1,10,1)
For(Y,1,26,1)
0→[A](Y,X)
0→[B](Y,X)
End:End
For(X,1,26,1)
For(Y,1,10,1)
Output(Y,X,"!")
End:End
Lbl 4
0→S:0→W
ClrHome
Menu("WELCOME","CREATE",99,"PRESETS",11,"INFOS",88,"QUIT",55
Lbl 11
Menu("PRESETS","HEART",50,"STUCK ON 8",51,"PYRAMID",53,"INSANE FIG!",54,"RETURN",4
Lbl 55
ClrHome:Stop
Lbl 99
ClrHome
Disp "POSITIONS ON"
Disp "ENTER X (MAX 9)"
Repeat G=92 or G=93 or G=94 or G=82 or G=83 or G=84 or G=72 or G=73 or G=74 or G=105
getKey→G
End
If G=105:Then:Goto 7:End
If G=92 or G=93 or G=94
Then:G-90→U:End
If G=82 or G=83 or G=84
Then:G-77→U:End
If G=72 or G=73 or G=74
Then:G-64→U:End
Disp "ENTER Y (MAX 9)"
Repeat G=92 or G=93 or G=94 or G=72 or G=73 or G=74 or G=82 or G=83 or G=84
getKey→G
End
If G=92 or G=93 or G=94
Then:G-90→V:End
If G=82 or G=83 or G=84
Then:G-77→V:End
If G=72 or G=73 or G=74
Then:G-64→V:End
1→[A](V,U)
Goto 99
Lbl 7
ClrHome:1→C:0→B
For(X,12,26,1)
Output(2,X,"▫")
Output(5,X,"▫")
Output(8,X,"▫"):End
Output(9,12,"­­PRESS A KEY­­"
Output(10,12,"­­­­TO STOP­­­­"
Output(4,14,"RENDER")
Output(4,23,"/65"
Output(3,13,"GENERATION
Output(1,14,"GAME OF LIFE")
Output(6,13,"ON:")
Output(7,13,"OFF:"
For(X,1,10,1):Output(X,10,"•▫":End
Lbl 8
Repeat getKey
Output(6,17,S)
Output(7,17,W)
0→S:0→W
If C=65:Then:1→C:End
B+1→B
Output(3,25,B)
For(Y,2,9,1)
For(X,2,9,1)
C+1→C
Output(4,21,"  ")
Output(4,21,C
If [A](Y,X)=1
Then
Output(Y-1,X-1,"X")
Else
Output(Y-1,X-1," ")
End
[A](Y-1,X-1)+[A](Y,X-1)+[A](Y+1,X-1)+[A](Y-1,X)+[A](Y+1,X)+[A](Y-1,X+1)+[A](Y,X+1)+[A](Y+1,X+1)→N
If ([A](Y,X) and (N=2 or N=3)) or (not([A](Y,X)) and N=3)
Then
1→[B](Y,X):S+1→S
Else
0→[B](Y,X):W+1→W
End
End
End
[B]→[A]
End
Goto 4
Lbl 50
1→[A](1,2):1→[A](1,6)
1→[A](2,4):1→[A](3,2)
1→[A](3,6):1→[A](4,3)
1→[A](4,4):1→[A](4,5)
Goto 7
Lbl 51
1→[A](1,1):1→[A](1,3):1→[A](1,5):1→[A](1,7)
1→[A](2,2):1→[A](2,4):1→[A](2,6):1→[A](2,8)
1→[A](3,1):1→[A](3,3):1→[A](3,5):1→[A](3,7)
Goto 7
Lbl 53
1→[A](2,5):1→[A](3,4):1→[A](3,5)
1→[A](3,6):1→[A](4,3):1→[A](4,7)
1→[A](5,2):1→[A](5,3):1→[A](5,4)
1→[A](5,5):1→[A](5,6):1→[A](5,7)
1→[A](5,8):1→[A](6,2):1→[A](6,4)
1→[A](6,6):1→[A](6,8):Goto 7
Lbl 54
For(X,2,10,1)
1→[A](5,X):End
Goto 7
Lbl 88
ClrHome
Menu("INFOS MENU","ABOUT THE GAME",71,"ABOUT THE AUTHOR",72,"ABOUT THIS VERSION",73,"RETURN",4
Lbl 73
ClrHome
Disp "MATRIX RUNING ON 9X9"
Disp "LOW SPEED BECAUSE
Disp "IT'S A SMALL PROCESSOR"
Disp "(Z80 AT 6 MHZ)
Disp "PRESS ENTER TO STOP PROG"
Goto 33
Lbl 72
ClrHome
Disp "CREATED BY MOOGGY
Disp ""
Disp "BASED ON ROSETTA CODE VERS
Disp "HELPS BY OPEN CLASSROOMS
Disp ""
Disp "(C)­2017"
Goto 33
Lbl 71
ClrHome
Disp "INVENTED IN 1970,"
Disp "THE G­OF­L IS SIMPLE:"
Disp "IT'S AN ALGORYTHM RUNNING
Disp "LIKE THIS:"
Disp "3 ALIVE=BORN"
Disp "2 ALIVE=NOTHING"
Disp "1/4+ ALIVE=DIE"
Lbl 33
Repeat getKey
Output(10,1,"PRESS A KEY
Wait 0.2
Output(10,1,"           
Wait 0.1:End:Goto 88
